<?xml version="1.0" encoding="utf-8"?>
<mx:Module xmlns:mx="http://www.adobe.com/2006/mxml"
		   xmlns:mpi="nl.mpi.lexus.*"
		   layout="vertical"
		   width="100%"
		   height="100%"
		   xmlns:local="*"
		   xmlns:view="nl.mpi.lexus.view.*">

	<mx:Script>
		<![CDATA[
			import com.adobe.serialization.json.JSON;
			
			import mx.binding.utils.BindingUtils;
			import mx.collections.ArrayCollection;
			import mx.collections.ICollectionView;
			import mx.collections.IViewCursor;
			import mx.collections.XMLListCollection;
			import mx.controls.Alert;
			import mx.controls.Label;
			import mx.controls.Text;
			import mx.controls.textClasses.TextRange;
			import mx.core.Container;
			import mx.core.DragSource;
			import mx.core.FlexGlobals;
			import mx.core.IUIComponent;
			import mx.events.CloseEvent;
			import mx.events.CollectionEvent;
			import mx.events.DragEvent;
			import mx.events.IndexChangedEvent;
			import mx.events.ListEvent;
			import mx.events.MenuEvent;
			import mx.managers.DragManager;
			import mx.managers.PopUpManager;
			import mx.messaging.SubscriptionInfo;
			import mx.rpc.events.ResultEvent;
			
			import nl.mpi.lexus.AutoSizeTree;
			import nl.mpi.lexus.LexusUtil;
			import nl.mpi.lexus.YesNo;
			import nl.mpi.lexus.event.NewSchemaElementEvent;
			import nl.mpi.lexus.event.NewSchemaRestrictionEvent;
			import nl.mpi.lexus.event.ViewChangeEvent;
			import nl.mpi.lexus.service.LexusService;
			import nl.mpi.lexus.view.ViewsEditor;

			[Bindable]
			private var application:Object = FlexGlobals.topLevelApplication;
			
			[Bindable]
			private var lexiconId:String;

			[Bindable]
			private var serverData:Object = new Object();

			private var dcOrder:ArrayCollection = new ArrayCollection();

			private var lexusService:LexusService = new LexusService();

			/**
			 * Marks whether the current schema information sould be saved.
			 **/
			private var schemaIsModified:Boolean = false;

			/**
			 * Maximum length for the title at the top of the screen
			 **/
			[Bindable]
			private var maxTitleLength:int = 140;
			
			private var dragSourceSelectedItem:Object = null;


			/**
			 * Is the lexicon editable?
			 **/
			[Bindable]
			private var editable:Boolean = false;
			private var restrictionFound:Boolean = false;

			/**
			 * A place to save an event. In case we need to save some data
			 *  while processing an event we park the original event here.
			 **/
			private var originalEvent:Event;
		
			[Bindable]
			private var _restrictions:Array;
			private var _invalidExports:Array;

			public function get invalidExports():Array
			{
				return _invalidExports;
			}

			public function set invalidExports(value:Array):void
			{
				_invalidExports = value;
			}

			public function get restrictions():Array
			{
				return _restrictions;
			}

			public function set restrictions(value:Array):void
			{
				_restrictions = value;
			}

			public function openLexicon(a_lexiconId:String):void {
				this.lexiconId = a_lexiconId;
				var param:Object = new Object();
				LexusUtil.showWait(this, "Loading structure");
				param.id = a_lexiconId;
				lexusService.send("LexusSchemaEditor/getSchema.json", param, this.name, onJSONLoad);
				}


			/**
			 * Event Handler for the onLoad event. This will parse the JSON object and perform initialization
			 * */
			private function onJSONLoad(event:ResultEvent):void {
				LexusUtil.parseJSON(String(event.result), function(data:Object):void {
					serverData = data;
					tree.dataProvider = serverData.result.schema;
					tree.selectedItem = (tree.dataProvider as ArrayCollection).getItemAt(0);
					editable = serverData.result.lexicon.writable;
//					exports = serverData.result.exports;
					schemaElementViewer.init(serverData.result.schema, editable);
					schemaElementViewer.addEventListener("change", onChange);
					toggleButtonBar();
					callLater(expandTree);
					schemaIsModified = false;
					viewsEditor.lexiconId = lexiconId;
					viewsEditor.editable = editable;
					if(rightTabNavigator.selectedChild == viewsEditor && viewsEditor.viewEditor.viewXML == null)
						viewsEditor.getViews();
					else
						LexusUtil.removeWait();

				});
				FlexGlobals.topLevelApplication.moduleInitialised("schema_editor");
			}


			public function extractDataCategory(a_id:String, a_schemaElement:Object):Object {
				if (a_schemaElement.id == a_id)
					return a_schemaElement;
				if (a_schemaElement.hasOwnProperty("children")) {
					var children:ArrayCollection = new ArrayCollection(a_schemaElement.children);
					for (var i:int = 0; i < children.length; i++) {
						var s:Object = this.extractDataCategory(a_id, children.getItemAt(i));
						if (s != null)
							return s;
					}
				}
				return null;
			}

			private function expandTree():void {
				tree.expandChildrenOf(this.serverData.result.schema, true);
			}

			/**
			 * Handles the event for changing the right tab
			 **/
			private function rightTabChanged(evt:IndexChangedEvent):void {
				if(this.editable)
					this.tree.dragEnabled = true;

				if (rightTabNavigator.selectedChild == viewsEditor){
					downBtn.visible = false;
					upBtn.visible = false;

					
					if (this.schemaIsModified) {
						this.originalEvent = evt;
						this.confirmSave();
					}else if( viewsEditor.viewEditor.viewXML == null) {
						viewsEditor.getViews();
					}
					
				}
				if (rightTabNavigator.selectedChild == schemaElementTab){
					downBtn.visible = true;
					upBtn.visible = true;
					if (viewsEditor.viewEditor.needsSave && !viewsEditor.viewEditor.isAlerting && rightTabNavigator.selectedChild != viewsEditor){
						YesNo.confirmSave("Do you want to save the view?", 
							function(event:CloseEvent):void {
								if (event.detail == Alert.YES) {
									viewsEditor.viewEditor.save();  
								}else if (event.detail == Alert.NO) {
									viewsEditor.viewEditor.discard();
								}
							}
						);
					}
				}

			}

			private function confirmSave():void {
				YesNo.confirmSave("Do you want to save your changes?", confirmSaveHandler);
			}

			private function confirmSaveHandler(event:CloseEvent):void {
				if (event.detail == Alert.YES) {
					save();
				} else {
					FlexGlobals.topLevelApplication.enabled = false;
					LexusUtil.showWait(this, "Reloading structure");
					openLexicon(serverData.result.lexicon.id);
					if(rightTabNavigator.selectedChild == viewsEditor && viewsEditor.viewEditor.viewXML == null)
						viewsEditor.getViews();
				}
				this.schemaIsModified = false; // HHV: hack hack
				viewsEditor.viewEditor.needsSave = false; // HHV: hack hack

				/**
				 * If the schema was saved and reloaded again and there's an outstanding menu-event,
				 * dispatch the event again.
				 **/
				if (this.originalEvent != null) {
					callLater(FlexGlobals.topLevelApplication.menuBar.dispatchEvent, [this.originalEvent]);
					this.originalEvent = null;
				}
			}

			/**
			 * Event handler for the Menu control's click event.
			 **/
			public function onMenuSelect(event:MenuEvent):Boolean {
				var handled:Boolean = true;
				this.originalEvent = null;
				var evtName:String = event.item.@eventName;
				if (evtName == "save") {
					save();
					return true;
				}
				//Request Save actions for other menu items
				if (this.schemaIsModified || viewsEditor.viewEditor.needsSave) {
					this.originalEvent = event;
					this.confirmSave();
					return true;
				}

				switch (evtName) {
					case "lexicon_browser":
					case "workspace":
					case "about":
						handled = false;
						break;
					default:
						handled = false;
						break;
				}
				return handled;
			}

			/* Save unsaved data, called from menu event handler or keyboard handler */
			public function save():void {
				if (this.schemaIsModified) {
					FlexGlobals.topLevelApplication.enabled = false;
					LexusUtil.showWait(this, "Saving structure");
					var param:Object = new Object();
					param.schema = this.serverData.result.schema;
					param.template = this.serverData.result.lexicon.template;
					param.listView = this.dcOrder.toArray();
					param.viewId = serverData.result.lexicon.id
					this.lexusService.send("LexusSchemaEditor/saveSchema.json", param, this.name, function():void {
						LexusUtil.removeWait();
						//reload the schema to get the new id's so they can be used in the views
						openLexicon(lexiconId);
						if(rightTabNavigator.selectedChild == viewsEditor && viewsEditor.viewEditor.viewXML == null)
							viewsEditor.getViews();
						else
							viewsEditor.viewEditor.structureTree.invalidateDisplayList();
					});
					this.schemaIsModified = false;
				}
				if (viewsEditor.viewEditor.needsSave) {
					viewsEditor.viewEditor.save();
				}
			}

			/* Clickerdeeclick */
			private function treeItemClicked(evt:ListEvent):void {
				schemaElementViewer.removeEventListener("change", onChange);				
				var selectedNode:Object = tree.selectedItem;
				schemaElementViewer.init(selectedNode, editable);
				schemaElementViewer.addEventListener("change", onChange);
				this.toggleButtonBar();
			}

			//AAM: Fixes ticket #1679
			private function changeAddButtonStatus():void{
				if(editable == true){
					var item:Object = tree.selectedItem;
					if (item != null && (item.type == "data category" || item.name =="lexicon")) {
						addBtn.source = application.addIconGrayed;
						addBtn.enabled = false;
						addBtn.toolTip = "It is not possible to add anything to a data category.";
					} else{
						addBtn.source = application.addIcon;
						addBtn.enabled = true;
						addBtn.toolTip = "Create new element.";
					}
				}
			}
			
			private function changeRemoveButtonStatus():void{
				if(editable == true){
					var item:Object = tree.selectedItem;
					if (item != null && (item.name =="lexicon" || item.name =="lexicalEntry")) {
						removeBtn.source = application.removeIconGrayed;
						removeBtn.enabled = false;
						removeBtn.toolTip = "The selected item '" + item.type + "' can not be deleted";
					} else{
						removeBtn.source = application.removeIcon;
						removeBtn.enabled = true;
						removeBtn.toolTip = "Remove element.";
					}
				}
			}
			
			private function changeUpButtonStatus():void{
				if(editable == true){
					var item:Object = tree.selectedItem;
					if (item != null && (item.name =="lexicon" || item.name =="lexicalEntry")) {
						upBtn.source = application.upIconGrayed;
						upBtn.enabled = false;
						upBtn.toolTip = "The selected item '" + item.type + "' can not be moved Up";
					} else{
						upBtn.source = application.upIcon;
						upBtn.enabled = true;
						upBtn.toolTip = "Move element Up. ";
					}
				}
			}
			
			private function changeDownButtonStatus():void{
				if(editable == true){
					var item:Object = tree.selectedItem;
					if (item != null && (item.name =="lexicon" || item.name =="lexicalEntry")) {
						downBtn.source = application.downIconGrayed;
						downBtn.enabled = false;
						downBtn.toolTip = "The selected item '" + item.type + "' can not be moved Down";
					} else{
						downBtn.source = application.downIcon;
						downBtn.enabled = true;
						downBtn.toolTip = "Move element Down.";
					}
				}
			}
			
			
			private function onStyleChange(event:Event):void {
				this.schemaIsModified = true;
			}

			/**
			* Toggles the buttonbar below the schema tree according to the schemaElement properties
			**/
			private function toggleButtonBar():void {
				addBtn.enabled = editable;
				upBtn.enabled = editable;
				downBtn.enabled = editable;
				removeBtn.enabled = editable;
				
				if (editable == false){
					addBtn.source = application.addIconGrayed;
					addBtn.toolTip = "This schema cannot be edited due to insufficient rights.";
					removeBtn.source = application.removeIconGrayed;
					removeBtn.toolTip = "This schema cannot be edited due to insufficient rights.";
					upBtn.source = application.upIconGrayed;
					upBtn.toolTip = "This schema cannot be edited due to insufficient rights.";
					downBtn.source = application.downIconGrayed;
					downBtn.toolTip = "This schema cannot be edited due to insufficient rights.";
				} else{
					this.changeAddButtonStatus();
					this.changeRemoveButtonStatus();
					this.changeUpButtonStatus();
					this.changeDownButtonStatus();
				}

			}

			private function onChange(event:Event):void {
				var idx:int = tree.selectedIndex;
				var item:Object = tree.selectedItem;
				this.schemaIsModified = true;
			//	YesNo.alert("You tried to change somthing in the schema tree");
				LexusUtil.forceTreeRedraw(tree, tree.dataProvider);
			}

			private function addSchemaElement():void {
				var item:Object = tree.selectedItem;
				//AAM: do  nothing if selected item is a datacategory.
				if (item.type == "data category") {
					//YesNo.alert("It is not possible to add anything to a data category.");
					return;
				} else if (item.type == "Lexicon") {
					YesNo.alert("It is not possible to add anything to the 'lexicon' element.");
					return;
				}
				
				if (schemaRestrictionsCheck("create", tree.selectedItem))
				{
					var popAdd:AddSchemaElement = AddSchemaElement(PopUpManager.createPopUp(this, AddSchemaElement, true));
					popAdd.futureSibblings = new ArrayCollection(tree.selectedItem.children);
					popAdd.addEventListener("new", this.handleNewSchemaElement);
					PopUpManager.centerPopUp(popAdd);
				}else
				{
					var pop:RestrictionAlert = RestrictionAlert(PopUpManager.createPopUp(application as DisplayObject,RestrictionAlert,true));
					
					pop.restrictions = new ArrayCollection(restrictions);
					pop.addEventListener("proceed",confirmCreateHandler);
					PopUpManager.centerPopUp(pop);
				
				
				}
			}
			
			private function fetchValidExports():Array{
			
				var valexp:Array = new Array;
			if (this.serverData.result.lexicon.template.export != null)
			{	for each (var exp in this.serverData.result.lexicon.template.export)
					{if (exp.valid)
								valexp.push(exp.id);
					} 
			}
			return valexp;
			}
			
			
			public function schemaRestrictionsCheck(action:String, node:Object):Boolean{
				
				restrictionFound = false;
				
				var restriction:Object;
				var validExports:Array= fetchValidExports(); 
				var status:String;
				restrictions = new Array();
				invalidExports = new Array();
				if (node.src == "template")
				for each (var item in validExports)
				{	if (node.editor != null){
						for each (var rest in node.editor.restriction)
						{ if (item == rest.exports && rest.scope=="self" )
							for each (var act in rest.action) 
							{ if (act.type == action && act.cause != null)
							{		restriction = act;
									restrictionFound = true;
									status="restricted";
									restrictions.push(act);
									if (act.cause.toUpperCase() == "WARNING")
										invalidExports.push(item);
									break;
							}
						}
						}
						
						}
						
						if (restriction == null)
							{ //nearest ancestor should be checked. 
						
							var ancestor:Object = tree.getParentItem(node);
							    restrictionFound = false;	
								
								while (!restrictionFound && ancestor !=null)
								{						
								 if (ancestor.editor != null){
									for each (var rest in ancestor.editor.restriction)
									{ if (item == rest.exports && rest.scope == node.type)
										for each (var act in rest.action) 
									{ 		if (act.type == action && act.cause != null)
											{	
												restriction = act;
												restrictionFound = true;
												status="restricted";
												restrictions.push(act);
												if (act.cause.toUpperCase() == "WARNING")
													invalidExports.push(item);
												break;
									}
									}
									}}
								 ancestor = tree.getParentItem(ancestor)
								}
						
							}
						else if (restriction.cause == null || restriction == null)
								{ //action is explicitally allowd to override an ancestor 
								
								status = "allowed";
								}
				}	
			if (restrictions.length >0)	
				return false;
			else
				return true
			}
			
			private function confirmCreateHandler(event:NewSchemaRestrictionEvent):void {
				if (event.response == "yes") {
					var popAdd:AddSchemaElement = AddSchemaElement(PopUpManager.createPopUp(this, AddSchemaElement, true));
					popAdd.futureSibblings = new ArrayCollection(tree.selectedItem.children);
					popAdd.addEventListener("new", this.handleNewSchemaElement);
					PopUpManager.centerPopUp(popAdd);
					invalidateExport();
				
				}
			}
			
			

			private function handleNewSchemaElement(event:NewSchemaElementEvent):void {
				var item:Object = tree.selectedItem;
				if (item.type == "data category")
					return;
				if (event.schemaElement.name == ""){
					YesNo.alert("You must specify a non-empty name for new schema elements!");
					return;
				}
				
				var schemaEl:Object;
				var children:ArrayCollection = new ArrayCollection(item.children);
				var index:int = -1;
				//Inserting the data category at the end of the data category list
				if (event.schemaElement.type == "data category") {
					for (var i:int = 0; i < children.length; i++) {
						schemaEl = children.getItemAt(i);
						
						if (event.schemaElement.name == schemaEl.name && schemaEl.type == "data category"){
							YesNo.alert("There is already a data category with the same name you specified! The names of the lexicon schema elements must be unique.");
							return;
						}
						
						if (schemaEl.type != "data category") {
							index = i;
							break;
						}
					}
				} else {
					for (var j:int = 0; j < children.length; j++) {
						schemaEl = children.getItemAt(j);
						
						if (event.schemaElement.name == schemaEl.name && schemaEl.type == "container"){
							YesNo.alert("There is already a data container with the same name you specified! The names of the lexicon schema elements must be unique.");
							return;
						}
					}
				}
				if (index == -1)
					children.addItem(event.schemaElement);
				else
					children.addItemAt(event.schemaElement, index);
				//SSha: enhanced feature request update to have a datacategory folder open and unfolded upon creation.
				tree.expandChildrenOf(event.schemaElement, true);
				
				this.schemaIsModified = true;
				LexusUtil.forceTreeRedraw(tree, tree.dataProvider);
			}

			private function removeSchemaElement():void {
				if (! schemaRestrictionsCheck("delete",tree.selectedItem)){
					var mess:String=new String("Lexical entry information for this " + tree.selectedItem.type + " will be permanently lost!!\n\n Would You Still like to proceed?");
					var pop:RestrictionAlert = RestrictionAlert(PopUpManager.createPopUp(application as DisplayObject,RestrictionAlert,true));
					
					pop.restrictions = new ArrayCollection(restrictions);
					pop.addEventListener("proceed",confirmProceedWithDeleteHandler);
					PopUpManager.centerPopUp(pop);
					
					
				}
				else
				YesNo.confirmDelete("Are you sure you want to\nremove this schema element?\nLexical entry information for this " + tree.selectedItem.type + " will be permanently lost!!", confirmDeleteHandler);

			}
	
			private function confirmProceedWithDeleteHandler(event:NewSchemaRestrictionEvent):void {
				if (event.response == "yes") {
					var selItem:Object = tree.selectedItem;
					var parentElement:Object = tree.getParentItem(selItem);
					var children:ArrayCollection = new ArrayCollection(parentElement.children);
					children.removeItemAt(children.getItemIndex(selItem));
					schemaIsModified = true;
					
					tree.selectedItem = parentElement;
					LexusUtil.forceTreeRedraw(tree, tree.dataProvider);
					schemaElementViewer.init(tree.selectedItem, editable);
					this.toggleButtonBar();
					invalidateExport();
				
				}
				
				
			}
			
			private function confirmDeleteHandler(event:CloseEvent):void {
				if (event.detail == Alert.YES) {
					var selItem:Object = tree.selectedItem;
					var parentElement:Object = tree.getParentItem(selItem);
					var children:ArrayCollection = new ArrayCollection(parentElement.children);
					children.removeItemAt(children.getItemIndex(selItem));
					schemaIsModified = true;

					tree.selectedItem = parentElement;
					LexusUtil.forceTreeRedraw(tree, tree.dataProvider);
					schemaElementViewer.init(tree.selectedItem, editable);
					this.toggleButtonBar();
				}
			}
	
			public function invalidateExport():void{
				
				for each (var invexp in invalidExports)
					{	for each (var exp in this.serverData.result.lexicon.template.export)
							if (invexp == exp.id)
								 exp.valid = "false";
							
					}
			}
			/**
			 * Returns the icon class for the specified schemaElement. This method is used by the List
			 * of currently active schema elements to determine the icons associated with the schema elements
			 **/
			private function getDataCategoryIconClass(schemaElement:Object):Class {
				return FlexGlobals.topLevelApplication.dataCategoryIcon;
			}

			private function mayAddSchemaElement(schemaElement:Object):Boolean {
				var mayAdd:Boolean = true;
				if (schemaElement.type != "data category")
					mayAdd = false;
				else {
					for (var i:int = 0; i < this.dcOrder.length; i++) {
						if (this.dcOrder.getItemAt(i) == schemaElement) {
							mayAdd = false;
							break;
						}
					}
				}
				return mayAdd;
			}

			private function generateSchemaElementView(schemaElement:Object, parent:Container):void {
				if (schemaElement == null) // HHV: this sometimes happens, maybe due to db-data corruption/bug or bad timing.
					return;
				var names:ArrayCollection = new ArrayCollection();
				names.addItem(schemaElement.name);

				this._generateSchemaElementView(names, schemaElement, parent);

			}

			private function _generateSchemaElementView(labels:ArrayCollection, schemaElement:Object, parent:Container):void {
				var _parent:Container = new HBox();
				_parent.setStyle("verticalAlign", "bottom");
				_parent.addEventListener(MouseEvent.MOUSE_OVER, this.onRollOver);
				_parent.addEventListener(MouseEvent.MOUSE_OUT, this.onMouseOut);
				parent.addChild(_parent);

				if (labels.length > 1 && schemaElement.style.textBeforeGroup != null) {
					var preGroupTxt:Text = new Text();
					preGroupTxt.text = schemaElement.style.textBeforeGroup;
					_parent.addChild(preGroupTxt);

				}

				for (var i:int = 0; i < labels.length; i++) {
					if (schemaElement.style.textBefore != null) {
						var preTxt:Text = new Text();
						preTxt.text = schemaElement.style.textBefore;
						_parent.addChild(preTxt);
					}
					var txt:Text = new Text();
					txt.text = "";
					if (schemaElement.style.textBeforeUseStyle != null)
						txt.text += schemaElement.style.textBeforeUseStyle;
					if (schemaElement.style.smallCaps)
						txt.text += (labels.getItemAt(i) as String).toLowerCase();
					else if (schemaElement.style.allCaps)
						txt.text += (labels.getItemAt(i) as String).toUpperCase();
					else
						txt.text += labels.getItemAt(i);
					if (schemaElement.style.textAfterUseStyle != null)
						txt.text += schemaElement.style.textAfterUseStyle;

					txt.setStyle("fontFamily", schemaElement.style.fontFamily);
					txt.setStyle("fontSize", schemaElement.style.fontSize);
					txt.setStyle("color", schemaElement.style.textColor);

					schemaElement.style.bold ? txt.setStyle("fontWeight", "bold") : txt.setStyle("fontWeight", "normal");
					schemaElement.style.italic ? txt.setStyle("fontStyle", "italic") : txt.setStyle("fontStyle", "normal");
					schemaElement.style.underline ? txt.setStyle("textDecoration", "underline") : txt.setStyle("textDecoration", "none");

					_parent.addChild(txt);
					if (schemaElement.style.textAfter != null) {
						var postTxt:Text = new Text();
						postTxt.text = schemaElement.style.textAfter;
						_parent.addChild(postTxt);
					}

					if (i != labels.length - 1 && labels.length > 1 && schemaElement.style.punctuationText != null) {
						var punctTxt:Text = new Text();
						punctTxt.text = schemaElement.style.punctuationText;
						_parent.addChild(punctTxt);
					}
				}
				if (labels.length > 1 && schemaElement.style.textAfterGroup != null) {
					var postGroupTxt:Text = new Text();
					postGroupTxt.text = schemaElement.style.textAfterGroup;
					_parent.addChild(postGroupTxt);

				}
			}

			private function onRollOver(event:MouseEvent):void {
				event.currentTarget.setStyle("borderStyle", "outset");
			}

			private function onMouseOut(event:MouseEvent):void {
				event.currentTarget.setStyle("borderStyle", "none");
			}
			/**
			 * Handles the drag over event on the tree
			 **/
			private function onTreeDragOver( evt:DragEvent):void{
				
				evt.preventDefault();
				
				var dropTarget:AutoSizeTree = AutoSizeTree(evt.currentTarget);
				var initiator:IUIComponent = evt.dragInitiator;
				var dropEnabled:Boolean = false;
				if( initiator == tree && dropTarget == tree){
					var dropLocation:int = dropTarget.calculateDropIndex(evt);
					var newParent:Object = tree.mx_internal::_dropData.parent;
					var oldParent:Object = tree.getParentItem(dragSourceSelectedItem);
					var childrenOffset:int = dragSourceSelectedItem.children == null ? 1 :dragSourceSelectedItem.children.length + 1;
					
					if (newParent != null && oldParent != null && newParent == oldParent 
						&& newParent != dragSourceSelectedItem 
						&& tree.getItemIndex(dragSourceSelectedItem) != dropLocation 
						&& (tree.getItemIndex(dragSourceSelectedItem) + childrenOffset) != dropLocation)
						dropEnabled = true;
					else
						dropEnabled = false;
				}
				
				if (dropEnabled) {
					evt.currentTarget.showDropFeedback(evt);
					DragManager.showFeedback(DragManager.MOVE);
					DragManager.acceptDragDrop(dropTarget);	
					
				}
				else {
					evt.currentTarget.hideDropFeedback(evt);
					DragManager.showFeedback(DragManager.NONE);
				}
			}
			
			private function highlightNode(droppedItem:Object):void{
					tree.selectedIndex = tree.getItemIndex(droppedItem);
			
			} 
			
			private function onTreeDragEnter(event:DragEvent):void{
				this.onTreeDragOver(event);
				DragManager.acceptDragDrop(AutoSizeTree(event.currentTarget));	
			}
			
			private function onTreeDragComplete(event:DragEvent):void
			{
				viewsEditor.viewEditor.dragSourceFromSchemaEditor = null;
				event.currentTarget.hideDropFeedback(event);
				callLater(highlightNode,[dragSourceSelectedItem]);
				dragSourceSelectedItem = null;
				this.schemaIsModified = true;
				}
			private function moveElementUp(evt:Event):void {
				var element:Object = this.tree.selectedItem;
				var parent:Object = this.tree.getParentItem(element);
				if (parent != null) {
					var children:ArrayCollection = new ArrayCollection(parent.children);
					var index:int = children.getItemIndex(element);
					if (index > 0) {
						if (element.type != "data category") {
							var prevEl:Object = children.getItemAt(index - 1);
							if (prevEl.type == "data category")
								return;
						}
						children.removeItemAt(index);
						children.addItemAt(element, index - 1);
						this.schemaIsModified = true;
						LexusUtil.forceTreeRedraw(tree, tree.dataProvider);
					}
				}
			}

			private function moveElementDown(evt:Event):void {
				var element:Object = this.tree.selectedItem;
				var parent:Object = this.tree.getParentItem(element);
				if (parent != null) {
					var children:ArrayCollection = new ArrayCollection(parent.children);
					var index:int = children.getItemIndex(element);
					if (index < children.length - 1) {
						if (element.type == "data category") {
							var nextEl:Object = children.getItemAt(index + 1);
							if (nextEl.type != "data category")
								return;
						}
						children.removeItemAt(index);
						children.addItemAt(element, index + 1);
						this.schemaIsModified = true;
						LexusUtil.forceTreeRedraw(tree, tree.dataProvider);
					}
				}
			}

			private function getTreeIcon(element:Object):Class {
				if (element.type == "data category")
					return FlexGlobals.topLevelApplication.dataCategoryIcon;

				return FlexGlobals.topLevelApplication.containerIcon;

			}

			private function mouseDown(event:MouseEvent):void {
				viewsEditor.viewEditor.dragSourceFromSchemaEditor = tree.selectedItem;
				dragSourceSelectedItem = tree.selectedItem ;
			}

		]]>
	</mx:Script>

	<mx:VBox id="main"
			 width="100%"
			 height="100%"
			 verticalGap="0"
			 focusIn="FlexGlobals.topLevelApplication.overlayUnicodeInput(event)">
		<mx:Panel width="100%"
				  height="100%"
				  status="Schema and view editor"
				  statusStyleName="panelStatus"
				  title="{LexusUtil.cutOffString(serverData.result.lexicon.name + ': ' + serverData.result.lexicon.description, maxTitleLength)}"
				  titleStyleName="panelTitle"
				  backgroundAlpha="0.0"
				  borderAlpha="0.0">
			<mx:TabNavigator width="100%"
							 height="100%"
							 creationPolicy="all">
				<mx:HBox width="100%"
						 height="100%"
						 label="Lexicon structure">
					<mx:HBox width="25%"
							 height="100%">
						<mx:VBox width="100%"
								 height="100%">
							<mpi:AutoSizeTree horizontalScrollPolicy="auto"
											  id="tree"
											  width="100%"
											  height="100%"
											  dataProvider="{this.serverData.result.schema}"
											  dragEnabled="{this.editable}" dropEnabled="{this.editable}" dragMoveEnabled="{this.editable}" 
											  dragOver="onTreeDragOver( event)" dragEnter="onTreeDragEnter(event)"
											  dragComplete ="onTreeDragComplete(event)"
										 	  labelField="name"
											  change="treeItemClicked(event)"
											  mouseDown="mouseDown(event)" 
											  iconFunction="getTreeIcon"/>
							<mx:ControlBar horizontalAlign="center"
										   height="5%"
										   width="100%">
								<mx:Image id="addBtn"
										  source="{application.addIcon}"
										  click="addSchemaElement()"
										  toolTip="Create new element."/>
								<mx:Image id="removeBtn"
										  source="{application.removeIcon}"
										  click="removeSchemaElement()"
										  toolTip="Remove element."/>
							</mx:ControlBar>
						</mx:VBox>
						<mx:VBox width="5%"
								 height="100%"
								 verticalAlign="middle"
								 horizontalAlign="center">
							<mx:Image id="upBtn"
									  source="{application.upIcon}"
									  click="moveElementUp(event)"
									  toolTip="Move element up."/>
							<mx:Image id="downBtn"
									  source="{application.downIcon}"
									  click="moveElementDown(event)"
									  toolTip="Move element down."/>
						</mx:VBox>
					</mx:HBox>
					<mx:Canvas width="75%"
							   height="100%"
							   backgroundColor="#FEAF69">
						<mx:TabNavigator id="rightTabNavigator"
										 width="100%"
										 height="100%"
										 creationPolicy="all"
										 change="rightTabChanged(event)">
							<mx:Canvas id="schemaElementTab"
									   label="Schema element"
									   width="100%"
									   height="100%">
								<local:SchemaElement id="schemaElementViewer"
													 width="100%"
													 height="100%"/>
							</mx:Canvas>

							<view:ViewsEditor id="viewsEditor"
											  label="View editor"
											  width="100%"
											  height="100%"
											  schemaElementsTree="{tree}"/>
						</mx:TabNavigator>
					</mx:Canvas>
				</mx:HBox>
			</mx:TabNavigator>
		</mx:Panel>
	</mx:VBox>

</mx:Module>
