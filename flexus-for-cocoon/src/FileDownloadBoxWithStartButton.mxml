<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:mpi="nl.mpi.lexus.*"
	title="Download converted toolbox files." width="500" height="150" 
	paddingLeft="10" paddingRight="10" paddingBottom="10" paddingTop="10"
	showCloseButton="true" close="close()"
	creationComplete="init()"
	borderAlpha="1.0">
	<mx:Script>
		<![CDATA[
			import mx.managers.PopUpManager;
			
			import nl.mpi.lexus.LexusUtil;
						
			[Bindable] public var theUrl:String;
			[Bindable] public var startEnabled:Boolean;
			[Bindable] private var fileDownload:FileReference;
			
			private function init():void {
				startEnabled = false;
				fileDownload = new FileReference();
				fileDownload.addEventListener(ProgressEvent.PROGRESS, progressHandler);
				fileDownload.addEventListener(Event.COMPLETE, uploadCompleted);
			}			
			
			private function close():void {
				PopUpManager.removePopUp(this);
			}

			private function start():void {
				LexusUtil.hide(explanation);
				startEnabled = false;
				cancelDownloadButton.enabled = true;
				var request:URLRequest = new URLRequest();
				request.url = theUrl;
				fileDownload.download(request);
				LexusUtil.show(preDownloadProgress);
				preDownloadProgress.setProgress(0, 1);
			}
			
			private function uploadCompleted(event:Event):void {
				downloadProgress.setProgress(1, 1);
				downloadProgress.label = "Download completed";
				LexusUtil.show(closePopupButton);
				LexusUtil.hide(startDownload);				
				LexusUtil.hide(cancelDownloadButton);				
			}
			
			/**
			 * While the file is downloading, update the progress bar's status.
			 */
			private function progressHandler(event:ProgressEvent):void
			{
				LexusUtil.hide(preDownloadProgress);
				LexusUtil.show(downloadProgress);
				downloadProgress.setProgress(event.bytesLoaded, event.bytesTotal);
				downloadProgress.label = "%3%% done";

			}
			
			/**
			 * Immediately cancel the download in progress and disable the cancel button.
			 */
			public function cancelDownload():void {
				fileDownload.cancel();
				LexusUtil.hide(explanation);
				LexusUtil.hide(preDownloadProgress);
				LexusUtil.show(downloadProgress);
				downloadProgress.setProgress(1, 1);
				downloadProgress.label = "Download cancelled";
				cancelDownloadButton.enabled = false;
				LexusUtil.show(closePopupButton);
				LexusUtil.hide(startDownload);				
				LexusUtil.hide(cancelDownloadButton);
			}
		]]>
	</mx:Script>
	
	<mx:TextArea id="explanation" width="100%" height="100%" visible="{startEnabled}"
				 text="Your files have been uploaded, click {startDownload.label} to download your lexicon in Lexus format."/>
	<mx:ProgressBar id="preDownloadProgress" indeterminate="true" indeterminateMoveInterval="30" width="100%" visible="false" includeInLayout="false" trackHeight="10"/>
	<mx:ProgressBar id="downloadProgress" label="" mode="manual" width="100%" visible="false" includeInLayout="false"/>
	<mx:ControlBar horizontalAlign="center" width="100%">
		<mx:Button id="cancelDownloadButton" label="Cancel" click="cancelDownload();" enabled="true" />
		<mx:Spacer width="100%"/>
		<mx:Button id="closePopupButton" label="OK" click="close()" includeInLayout="false" visible="false"/>
		<mx:Spacer width="100%"/>
		<mx:Button id="startDownload" label="Start" click="start();" enabled="{startEnabled}" />
	</mx:ControlBar>
</mx:TitleWindow>
