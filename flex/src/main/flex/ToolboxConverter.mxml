<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml"
				layout="vertical"
				creationComplete="init()"
				title="Convert Toolbox files"
				showCloseButton="true"
				width="800" height="370" y="50"
				close="close();" xmlns:local="*">
<mx:Script>
	<![CDATA[
		import com.adobe.net.URI;
		import com.adobe.serialization.json.JSON;
		
		import mx.collections.ArrayCollection;
		import mx.managers.PopUpManager;
		import mx.utils.ArrayUtil;
		
		import nl.mpi.lexus.FileDownload;
		import nl.mpi.lexus.LexusUtil;
		import nl.mpi.lexus.YesNo;
		import nl.mpi.lexus.service.LexusService;
		
		private var urlRequest:URLRequest;
		[Bindable] private var typFileReference:FileReference;
		[Bindable] private var dataFileReference:FileReference;

		private var params:Object; // For sending the remote IDs to the converter
		private var popupDownloadConverted:FileDownloadBoxWithStartButton; // Used to download the converted toolbox files, after uploads are done.

		[Bindable] public var sessionId:String;
		
		
				
		private function init():void {
			params = new Object();
		}
		
		private function close():void {
			PopUpManager.removePopUp(this);
		}
		
		/*
		 * Delegate methods for UploadTwoFiles.
		 */
		
		public function getTitle(utf:UploadTwoFiles, fileNumber:int):String {
			return fileNumber == 1
				? "Select the file containing the structure of the Toolbox lexicon"
				: "Select the file containing the data of the Toolbox lexicon";
		}
		
		public function getBrowseTitle(utf:UploadTwoFiles, fileNumber:int):String {
			return fileNumber == 1
				? "Browse for .typ file"
				: "Browse for .lx file";
		}
		
		/*
		 * Get filefilter for browsing first file.
		 */
		public function getFilter1(utf:UploadTwoFiles):FileFilter
		{
			return new FileFilter("Toolbox", "*.typ");
		}
		/*
		 * Get filefilter for browsing second file.
		 */
		public function getFilter2(utf:UploadTwoFiles):FileFilter
		{
			return new FileFilter("Toolbox", "*.lx;*.txt");
		}
		
		/*
 		 * Return false if upload cannot start, true otherwise.
		 * Additional actions can be taken.
		 */
		public function uploadCanStart(utf:UploadTwoFiles, file_1_Reference:FileReference, file_2_Reference:FileReference):Boolean
		{
			try {
				if (file_1_Reference.name && file_2_Reference.name) {
					popupDownloadConverted = FileDownloadBoxWithStartButton(PopUpManager.createPopUp(this, FileDownloadBoxWithStartButton, false));
					PopUpManager.centerPopUp(popupDownloadConverted);
					return true;
				}
			} catch (error:Error) { // Skipping this 'cos both files must be selected first.
				YesNo.alert("Please select the .typ and .lx file first.");
				return false;
			}
			return false;
		}
		
		/*
		 * Get URLRequest object for specified file.
		 */
		public function getURLRequest(utf:UploadTwoFiles, fileNumber:int, file_Reference:FileReference):URLRequest {
			return new URLRequest(LexusService.getAbsoluteURL("StagingFileHandler/uploadFile.xml;jsessionid=" + this.sessionId));
		}
		
		public function uploadFile1Completed(utf:UploadTwoFiles, data:XML):void {
			params.typFileId = data.id;
		}
		public function uploadFile2Completed(utf:UploadTwoFiles, data:XML):void {
			params.dataFileId = data.id;
		}
		public function finished(utf:UploadTwoFiles):void {
			var url:String = LexusService.getAbsoluteURL("toolbox/convert/"+params.typFileId+"/"+params.dataFileId+"/ConvertedToolbox.zip");
			popupDownloadConverted.theUrl = url;
			popupDownloadConverted.startEnabled = true;
			var timer:Timer = new Timer(1000, 1);
			timer.addEventListener(flash.events.TimerEvent.TIMER,
				function():void {
					close();
				}
			);
			timer.start();
		}
	]]>
</mx:Script>
	<local:UploadTwoFiles id="utf" delegate="{this}"/>
</mx:TitleWindow>
